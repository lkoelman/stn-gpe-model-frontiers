TITLE Calcium buffering for GPe neuron

COMMENT

DESCRIPTION
-----------

Ca2+ buffering for GPe neuron

NEURON Implementation of GENESIS "Ca_concen" mechanism described in 
http://genesis-sim.org/GENESIS/Hyperdoc/Manual-26.html#ss26.1

Parameters taken from Hendrickson et al., 2010:
https://senselab.med.yale.edu/ModelDB/showmodel.cshtml?model=127728

USAGE
-----

If you want to use the Ca2+ concentrations calculated here to calculate 
the Ca reversal potential using the Nernst equation for use in other mechanisms 
that read eca, you can set the ion_style in the correspondic sections 
to [3,2,1,1,1]. (see 
https://neuron.yale.edu/neuron/static/new_doc/modelspec/programmatic/ions.html#ion_style)

Alternatively, you can calculate the Nernst potential explicitly from cai/cao
in those mechanims. For an example: see /nrn/share/examples/nrniv/nmodl/cachan.mod


CREDITS
-------

modeled in GENESIS by J.R. Edgerton, 2004
implemented in NEURON by Lucas Koelman, 2018 to reflect model Hendrickson et al., 2010

ENDCOMMENT

NEURON {
    SUFFIX Calcium
    USEION ca READ ica WRITE cai
    RANGE B, shell_vol, tau
}

UNITS {
    (mM) = (milli/liter)
    (mA) = (milliamp)
    (um) = (micron)
}

PARAMETER {
    
    cai0 = 5e-05 (mM)   : 'Ca_base' in Hendrickson model files
    tau = 1.0 (ms)      : 'tau_CaClearance' in Hendrickson model files
    B = 6.9333333e-12 (1/coulomb) : = 4.0/3.0*5.2e-12 (mol/coulomb) ('B' in Hendrickson model files)
                        : Not equal to Ca_concen.B, but value before division by shell volume
    shell_vol = 6.283185050026674e-08 (m3) : (m3) Hendrickson value for soma
    area (um2)          : segment area in micron
    
}

ASSIGNED {
    : read simulator variables
    ica (mA/cm2)
}

STATE {
    : assigned mechanism variables (differential equation)
    : ca_offset (mM)  : fluctuation around Ca_base

    : assigned simulator variables
    cai (mM)
}

INITIAL{
    : ca_offset = 0.0
    cai = cai0
}

BREAKPOINT {
    SOLVE integrate METHOD derivimplicit :cnexp
    : cai = cai0 + ca_offset
}

DERIVATIVE integrate {
    : negative membrane current = positive charge injection
    : ca_offset' = 1e-3 * (-B / shell_vol) * (area * 1e-11 * ica) - ca_offset / tau
    : -> ica is first converted to GENESIS units (Ampere)
    : -> B / shell_vol is in same units as GENESIS (moles/coulomb/m3)
    : => resulting units of first term RHS are (mM/s) 
    : -> convert to mM/ms: factor 1e-3

    : cai' = (B / shell_vol) * (area * -ica) * 1e14 - (cai - cai0) / tau : (mM / ms)
    : ([mM] / [ms]) = 1e14 * ([mol/C] / [m^3]) * ([um^2] * [mA/cm^2]) - ([mM]/[ms])

    cai' = (B / shell_vol) * (area * -ica) - (cai - cai0) / tau : (mM / ms)
}

